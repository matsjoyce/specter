# Prime number finder. Prints all prime numbers between the numbers the user inputs (min, then max).

# Min
        INP
        SUB ONE
        STA NUM

# Max
        INP
        STA MAX

# Main checking loop. Check each number from NUM to MAX.
TLOOP   LDA NUM

# Have we done all MAX numbers?
        SUB MAX
        BRZ HALT

# Increment to next number to check.
        LDA NUM
        ADD ONE
        STA NUM

# Reset divisor.
        LDA ONE
        STA DIV

# Check NUM for primeness by dividing all numbers from 2 to NUM - 1 into it.
DLOOP   LDA DIV

# Increment to next divisor.
        ADD ONE
        STA DIV

# Have we checked up to the number itself?
        LDA DIV
        SUB NUM
        BRZ PRIME

# Setup for divide function.
        LDA NUM

# Modulus function: accumulator % DIV.

MODULUS SUB DIV
        BRP MODULUS
        ADD DIV

# Remainder is now in the accumulator. If its zero, NUM is not prime.
        BRZ NPRIME
        BRA DLOOP

# If its prime, print it.
PRIME   LDA NUM
        OUT

# Go back to the top.
NPRIME  BRA TLOOP

# End of program.
HALT    HLT

NUM     DAT 1
DIV     DAT 1
ONE     DAT 1
MAX     DAT
